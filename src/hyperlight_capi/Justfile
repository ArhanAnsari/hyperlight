lib-suffix := if os() == "windows" { ".dll" } else { "" }
bin-suffix := if os() == "windows" { ".exe" } else { ".out" }
# TODO: use UMDH here for windows?
# https://docs.microsoft.com/en-us/windows-hardware/drivers/debugger/umdh
clang-checks := if os() == "windows" { "-Werror -Wunused-variable -Werror=sign-compare -Wincompatible-pointer-types" } else { "-Wall -Werror -Wunused-variable -Werror=sign-compare -Wincompatible-pointer-types" }
valgrind-command := if os() == "windows" { "echo 'valgrind not supported on windows'" } else { "./scripts/valgrind-command.sh" }
test-command := if os() == "windows" { "./scripts/test-command.bat" } else { "./scripts/test-command.sh" }
gen-suppressions:= if os() == "windows" { " " } else { "GEN_SUPPRESSIONS_ON=1 LOG_FILE_ON=1" }

default-target:= "debug"

cargo-update:
    # run cargo update --dry-run with nightly toolchain to pull deps from private cargo feeds
    # but do not actually perform any updates
    cargo +nightly update --dry-run -Z bindeps

build target=default-target: cargo-update
    cargo +nightly -Z bindeps build --verbose --profile={{ if target == "debug" {"dev"} else { target } }} 

build-test-rust target=default-target:  (build target)
    # tracing tests cannot run with other tests
    cargo +nightly  test --profile={{ if target == "debug" {"dev"} else { target } }} -Z bindeps -- --skip test_trace
    cargo +nightly test --profile={{ if target == "debug" {"dev"} else { target } }} -Z bindeps test_trace 

build-tests-capi target=default-target: (build target)
    clang \
    {{ clang-checks }} \
    {{ if target == "debug" { "-D DEBUG" } else { "-O3" } }} \
    tests_capi/*.c \
    tests_capi/munit/munit.c \
    -Iinclude \
    -I../hyperlight_host/include \
    -o bin/test_c{{bin-suffix}} \
    -l hyperlight_capi{{lib-suffix}} \
    -L../../target/{{target}} \

run-tests-capi target=default-target: (build-tests-capi target)
    {{test-command}} {{target}}

valgrind-tests-capi target=default-target: (build-tests-capi target)
    {{valgrind-command}} {{target}}
valgrind-tests-capi-gen-suppressions target=default-target: (build-tests-capi target)
    {{gen-suppressions}} {{valgrind-command}} {{target}}
